#_( ;; Allow this script to be executed directly
   "exec" "bb" "--classpath" "$(clojure -Spath -Sdeps '{:deps {io.aviso/pretty {:mvn/version "RELEASE"}}}')" "$0" "$@"
   )

(ns site
  (:require
   [babashka.curl :as curl]
   [clojure.tools.cli :refer [parse-opts summarize]]
   [clojure.java.io :as io]
   [clojure.java.shell :as sh]
   [cheshire.core :as json]
   [clojure.pprint :refer [pprint]]
   [clojure.edn :as edn]
   [io.aviso.ansi :as ansi]
   [clojure.string :as str])
  (:import
   (java.nio.file Files)
   (java.nio.file.attribute FileAttribute)))

(def ^:dynamic *debug* nil)

(defn error! [& msg]
  (apply println (apply str ansi/red-font "fail: " ansi/reset-font) msg))

(defn error-and-exit! [exit & msg]
  (apply error! msg)
  (System/exit exit))

(defn error-and-usage! [msg opts]
  (error! msg)
  (println "Usage:")
  (println (:summary opts))
  (System/exit 1))

(defn message! [& msg]
  (apply println (apply str ansi/bold-green-font "site:" ansi/reset-font) msg))

(def config-file
  (case (System/getProperty "os.name")
    "Linux" (io/file (System/getProperty "user.home") ".config/site/config.edn")
    (error-and-exit! 1 "Unsupported OS")))

(def config
  (merge
   {:pass-password-dir "site"}
   (when (.exists config-file)
     (edn/read-string (slurp config-file)))))

(defn get-password-from-pass [username]
  (let [dir (io/file
             (System/getProperty "user.home")
             (str ".password-store/" (:pass-password-dir config)))
        passfile (io/file dir (str username ".gpg"))]
    (when (.exists passfile)
      (do
        (message! "Extracting password from " (str passfile))
        (let [{:keys [exit out]} (sh/sh "gpg" "-d" (str passfile))]
          (when (zero? exit) (str/trim out)))))))

(defn get-password [username]
  (or
   (get-password-from-pass username)
   ;; TODO: Try other strategies, like pinentry
   ))

(defn local-token-file []
  (let [dir (io/file (System/getProperty "user.home") ".local/share/site")]
    (when-not (.exists dir)
      (when-not (.mkdirs dir)
        (error-and-exit! 1 (str "Failed to make directory: " dir))))
    (io/file dir "access-token.json")))

(defn configure
  "Set a configuration value"
  [opts]
  (let [[k v] (:arguments opts)]
    (if (#{ ;; default site host (plus optional scheme)
           "site-server"
           ;; default username to use
           "username"
           ;; pass subdirectory to load passwords from, defaults to 'site'
           "pass-password-dir"
           ;; canonical host
           "canonical-host"} k)

      (case (System/getProperty "os.name")
        "Linux" (let [dir (io/file (System/getProperty "user.home") ".config/site")]
                  (assert v)
                  (when-not (.exists dir)
                    (when-not (.mkdirs dir)
                      (error-and-exit! 1 (str "Failed to make directory: " dir))))
                  (spit (io/file dir "config.edn") (with-out-str (pprint (assoc config (keyword k) v))))
                  (message! (format "Set %s to '%s'" k v)))
        (error-and-exit! 1 "Unsupported OS"))

      (if k
        (error-and-exit! 1 (format "%s is not a configuration setting" key))
        (do
          (message! "Printing config")
          (pprint config))))))

(defn get-token
  "Request a token"
  [opts]
  (let [{:keys [server username password curl]
         :or {server (:site-server config)
              username (:username config)}}
        (:options opts)]

    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not username (error-and-exit! 1 "Username required"))

    (let [{:keys [status headers body]}
          (curl/post
           (format "%s/_site/token" server)
           {:headers {"content-type" "application/x-www-form-urlencoded"}
            :basic-auth [username (or (get-password username) (error-and-exit! 1 "Failed to get password"))]
            :body "grant_type=client_credentials"
            :throw false
            :debug false})]
      (cond
        (= status 401)
        (do
          (message! "Server response: " status " " body)
          (message! (format "Are your credentials valid? request-id is %s" (get headers "site-request-id"))))

        (= status 200)
        (let [f (local-token-file)
              data (json/decode body)
              expires (java.util.Date.
                       (+ (.getTime (java.util.Date.))
                          (* (get data "expires_in") 1000)))

              data (-> data
                       (assoc "username" username)
                       (assoc "expires" (.format java.time.format.DateTimeFormatter/ISO_INSTANT (.toInstant expires))))]

          (message! (format "Access token acquired for %s%s%s: %s%s%s"
                            ansi/yellow-font username ansi/reset-font
                            ansi/cyan-font (get data "access_token") ansi/reset-font))
          (message! (format "Token expires in %s seconds" (get data "expires_in")))
          (message! (format "Storing token in %s" f))
          (spit f (json/encode data {:pretty true}))
          (when curl
            (message! (format "Storing curl config in %s" curl))
            (spit curl (format "-H \"Authorization: Bearer %s\"\n-H \"Accept: application/json,*.*;q=0.8\"\n" (get data "access_token")))))

        :else
        (do
          (println "Server response:" status (str/trim body)))))))

(defn load-token []
  "Load token from local cache"
  (let [f (local-token-file)
        _ (when-not (.exists f)
            (error-and-exit! 1 "Please request a token with the 'get-token' subcommand"))
        {access-token "access_token" expires "expires" :as token}
        (json/decode (slurp f))]

    (if (.isBefore (java.time.Instant/parse expires) (java.time.Instant/now))
      (error-and-exit! 1 "Access token has expired. Please request a new token with the 'get-token' subcommand")
      token)))

(defn check-token
  "Check the validity of the locally cached token"
  [_]
  (let [token (json/decode (slurp (local-token-file)))
        expires (get token "expires")]
    (if (.isBefore (java.time.Instant/parse expires) (java.time.Instant/now))
      (message! "Access token has expired")
      (let [mins-left (.until (java.time.Instant/now)
                              (java.time.Instant/parse expires)
                              java.time.temporal.ChronoUnit/MINUTES)]
        (message! (format "Access token is owned by %s%s%s: %s%s%s"
                          ansi/yellow-font (get token "username") ansi/reset-font
                          ansi/cyan-font (get token "access_token") ansi/reset-font))
        (if (> mins-left 1)
          (message! (format "Access token expires in %d minutes" mins-left))
          (message! (format "Access token expires in %d seconds"
                            (.until (java.time.Instant/now)
                                    (java.time.Instant/parse expires)
                                    java.time.temporal.ChronoUnit/SECONDS))))))))

(defn clear-token
  "Clear the token to end the session"
  [_]
  (if (.delete (local-token-file))
    (message! "Token deleted")
    (when (.exists (local-token-file))
      (error! "Failed to delete token"))))

(defn list-users
  "List users"
  [opts]
  (let [{:keys [server]
         :or {server (:site-server config)}}
        (:options opts)]
    (let [{access-token "access_token"} (load-token)
          {:keys [status headers body]}
          (curl/get
           (format "%s/_site/users/" server)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "accept" "application/json"}
            :throw false})]
      (cond
        (= (quot status 100) 2)
        (println (json/encode (json/decode body) {:pretty true}))

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn reset-password
  "Reset a user's password"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server canonical-host username password]
         :or {server (:site-server config)
              canonical-host (:canonical-host config)}}
        (:options opts)]

    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not canonical-host (error-and-exit! 1 "Canonical host required"))
    (when-not username (error-and-usage! "User required" opts))

    (let [password (or password (get-password username))
          {:keys [status headers body]}
          (curl/put
           (format "%s/_site/users/%s/password" server username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   {"type" "Password"
                    "user" (format "https://%s/_site/users/%s" canonical-host username)
                    "password" password})
            :throw false})]
      (cond
        (= status 401)
        (error! "Server response: " status " " (str/trim body))

        (= (quot status 100) 2)
        (message! "Password reset")

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn put-user
  "Create or update a user"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server username password name]
         :or {server (:site-server config)}}
        (:options opts)]
    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not username (error-and-usage! "Username required" opts))
    (when-not name (error-and-usage! "Name required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/users/%s" server username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   {"type" "User"
                    "username" username
                    "name" name})
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id: " (get headers "site-request-id")))

        (= status 201)
        (message! "User created")

        (= status 204)
        (message! "User updated")

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id"))))

      (when (#{201 204} status) (reset-password opts)))))

(defn put-role
  "Create or update a role"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server role description]
         :or {server (:site-server config)}}
        (:options opts)]
    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not role (error-and-usage! "Role required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/roles/%s" server role)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "Role"
                            "name" role}
                     description (assoc "description" description)))

            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id: " (get headers "site-request-id")))

        (= status 201)
        (message! "Role created")

        (= status 204)
        (message! "Role updated")

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn list-roles
  "List roles"
  [opts]
  (let [{:keys [server]
         :or {server (:site-server config)}}
        (:options opts)]
    (let [{access-token "access_token"} (load-token)
          {:keys [status headers body]}
          (curl/get
           (format "%s/_site/roles/" server)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "accept" "application/json"}
            :throw false})]
      (cond
        (= (quot status 100) 2)
        (println (json/encode (json/decode body) {:pretty true}))

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn assign-role
  "Assign a role to a user"
  [opts]
  (let [{access-token "access_token" assigner "username"} (load-token)
        {:keys [server canonical-host username role justification]
         :or {server (:site-server config)
              canonical-host (:canonical-host config)}}
        (:options opts)]
    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not username (error-and-usage! "Username required" opts))
    (when-not role (error-and-usage! "Role required" opts))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/roles/%s/users/%s" server role username)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "UserRoleMapping"
                            "assignee" (format "https://%s/_site/users/%s" canonical-host username)
                            "assigner" (format "https://%s/_site/users/%s" canonical-host assigner)
                            "role" (format "https://%s/_site/roles/%s" canonical-host role)}
                     justification (assoc "businessJustification" justification)))
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id: " (get headers "site-request-id")))

        (= status 201)
        (message! "Assignment created")

        (= status 204)
        (message! "Assignment updated")

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn put-rule
  "Put a rule"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server name rule]
         :or {server (:site-server config)}}
        (:options opts)]
    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not name (error-and-usage! "Name required" opts))
    (when-not rule (error-and-usage! "Rule required" opts))
    (when-not (.isFile (io/file rule)) (error-and-exit! 1 "Rule file must be a file"))

    (let [{:keys [status headers body]}
          (curl/put
           (format "%s/_site/rules/%s" server name)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (slurp (io/file rule))
            :throw false})]
      (cond
        (= status 201)
        (message! (format "Rule created: %s" (get headers "location")))

        (= status 204)
        (message! (format "Rule updated: %s" (get headers "location")))

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn put-variant
  "Associate a variant representation to a resource"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server canonical-host resource variant]
         :or {server (:site-server config)
              canonical-host (:canonical-host config)}}
        (:options opts)]
    (when-not server (error-and-exit! 1 "Server not specified"))
    (when-not resource (error-and-usage! "Resource required" opts))
    (when-not variant (error-and-usage! "Variant required" opts))

    ;; TODO: Should return a 404, not 405!!!

    (let [{:keys [status headers body]}
          (curl/post
           (format "%s/_site/variants/" server)
           {:headers {"authorization" (format "Bearer %s" access-token)
                      "content-type" "application/json"}
            :body (json/generate-string
                   (cond-> {"type" "Variant"
                            "resource" resource
                            "variant" variant}))
            :throw false})]
      (cond
        (= status 401)
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Is your access-token valid?")
          (message! "To get a new access token, use the get-token sub-command")
          (message! "Request id: " (get headers "site-request-id")))

        (= status 201)
        (message! (format "Variant created: %s" (get headers "location")))

        (= status 204)
        (message! (format "Variant updated: %s" (get headers "location")))

        :else
        (do
          (error! "Server response: " status " " (str/trim body))
          (message! "Request id: " (get headers "site-request-id")))))))

(defn install-api
  "Upload and install an API"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server openapi]
         :or {server (:site-server config)}} (:options opts)

        _ (when-not openapi (error-and-usage! "OpenAPI file not specified" opts))
        _ (when-not (.exists (io/file openapi)) (error-and-exit! 1 "OpenAPI file not found"))
        _ (when-not (.isFile (io/file openapi)) (error-and-exit! 1 "OpenAPI argument is not a file"))

        {:keys [status headers body]}
        (curl/put
         (format "%s/_site/apis/test" server)
         {:headers {"authorization" (format "Bearer %s" access-token)
                    "content-type" "application/json"}
          :body (json/generate-string
                 (cond-> {"type" "OpenAPI"
                          "openapi" (slurp (io/file openapi))}))
          :throw false})]
    (cond
      (= status 204)
      (message! "API installed")

      :else
      (do
        (error! "Server response: " status " " (str/trim body))
        (message! "Request id: " (get headers "site-request-id"))))))

(defn sh [& args]
  (let [{:keys [exit out err]} (apply sh/sh args)]
    (when (pos? exit)
      (error! err))))

(defn create-tmp-dir []
  (let [tmpdir (Files/createTempDirectory
                "site-" (into-array FileAttribute []))]
    (message! "Created tmp dir: " (.toString tmpdir))
    tmpdir))

(defn content-type [filename]
  (when-let [[_ suffix] (re-matches #"[^\.]+(\..*)" filename)]
    (or
     (get {".js" "application/json"
           ".js.map" "application/json"
           ".css.map" "application/json"
           ".css" "text/css"
           ".html" "text/html"
           ".png" "image/png"} suffix)
     (error-and-exit! 1 "Failed to determine content-type for" filename (format "(suffix=%s)" suffix)))))

(defn install-swagger-ui
  "Install a distribution of Swagger UI"
  [opts]
  (let [{access-token "access_token"} (load-token)
        {:keys [server] :or {server (:site-server config)}} (:options opts)
        tmpdir (create-tmp-dir)
        outfile (.resolve tmpdir "swagger-ui.zip")]
    (message! "Downloading Swagger UI distribution: " (.toString outfile))
    (sh "curl" "-o" (.toString outfile) "https://codeload.github.com/swagger-api/swagger-ui/zip/v3.44.1")
    (message! "Extracting Swagger UI distribution: " (.toString outfile))
    (sh "unzip" (.toString outfile) "-d" (.toString tmpdir))
    (let [dir (.resolve tmpdir "swagger-ui-3.44.1")]
      (doseq [f (file-seq (.toFile dir))
              :when (.isFile f)]
        (let [rel-path (.relativize tmpdir (.toPath f))]
          (when (.startsWith rel-path "swagger-ui-3.44.1/dist/")
            (let [url (format "%s/swagger-ui/%s" server (.toString rel-path))
                  content-type (content-type (last (str/split (.toString rel-path) #"/")))]
              (when content-type
                (message! "Uploading" (.toString rel-path) "to" url)
                (let [{:keys [status headers body command]}
                      (curl/put
                       url
                       {:headers {"authorization" (format "Bearer %s" access-token)
                                  "content-type" content-type}
                        :body f
                        :throw true})]
                  (when-not (= status 200)
                    (error-and-exit! 1 (str "Server response: " status " " (str/trim body)))
                    (message! "Request id: " (get headers "site-request-id"))))))))))
    ;; TODO: Clean up tmp dir
    ))

(defn debugf
  "Write a debug message to the console, but only if *debug* is true."
  [& args]
  (when *debug*
    (println "DEBUG:" (apply format args))))

(def cli-options
  [["-s" "--server SERVER" "The address of the server you are managing"]
   ["-c" "--canonical-host HOST" "The canonical host for the server"]
   ["-d" "--debug" "Enable debug mode"]
   ["-v" "--verbose" "Verbose mode"]
   ["-h" "--help" "Show this summary"]])

(def commands
  [["configure"
    {:description (:doc (meta #'configure))
     :cli-options []
     :delegate configure}]

   ["get-token"
    {:description (:doc (meta #'get-token))
     :cli-options [["-K" "--curl FILE" "Store creds in curl config file"]]
     :delegate get-token}]

   ["check-token"
    {:description (:doc (meta #'check-token))
     :cli-options []
     :delegate check-token}]

   ["clear-token"
    {:description (:doc (meta #'clear-token))
     :cli-options []
     :delegate clear-token}]

   ["list-users"
    {:description (:doc (meta #'list-users))
     :cli-options []
     :delegate list-users}]

   ["put-user"
    {:description (:doc (meta #'put-user))
     :cli-options [["-n" "--name NAME" "The user's name"]
                   ["-u" "--username USERNAME" "The username of the user you are creating/updating"]
                   ["-p" "--password PASSWORD" "A temporary password for the user"]
                   ]
     :delegate put-user}]

   ["reset-password"
    {:description (:doc (meta #'reset-password))

     :cli-options [["-u" "--username USERNAME" "The username of the user"]
                   ["-p" "--password PASSWORD" "A temporary password for the user"]]
     :delegate reset-password}]

   ["put-role"
    {:description (:doc (meta #'put-role))
     :cli-options [["-r" "--role ROLE" "The role you are creating/updating"]
                   ["-d" "--description DESCRIPTION" "An optional description"]]
     :delegate put-role}]

   ["list-roles"
    {:description (:doc (meta #'list-roles))
     :cli-options []
     :delegate list-roles}]

   ["assign-role"
    {:description (:doc (meta #'assign-role))
     :cli-options [["-u" "--username USERNAME" "The user receiving the role"]
                   ["-r" "--role ROLE" "The role you are assigning"]
                   ["-j" "--justification JUSTIFICATION" "The business justification"]]
     :delegate assign-role}]

   ["put-rule"
    {:description (:doc (meta #'put-rule))
     :cli-options [["-n" "--name NAME" "Rule name"]
                   ["-r" "--rule FILE" "Rule file"]]
     :delegate put-rule}]

   ["put-variant"
    {:description (:doc (meta #'put-variant))
     :cli-options [["-r" "--resource URL" "Resource"]
                   ["-v" "--variant URL" "Variant resource"]]
     :delegate put-variant}]

   ["install-api"
    {:description (:doc (meta #'install-api))
     :cli-options [["-f" "--openapi FILE" "OpenAPI description file"]]
     :delegate install-api}]

   ["install-swagger-ui"
    {:description (:doc (meta #'install-swagger-ui))
     :cli-options []
     :delegate install-swagger-ui}]

   ])

(let [{:keys [options arguments summary errors]}
      (parse-opts *command-line-args*  cli-options :in-order true)
      command (first arguments)
      {:keys [cli-options delegate]} ((into {} commands) (first arguments))

      print-summary
      (fn []
        (do
          (println (str ansi/bold-green-font "site" ansi/reset-font))
          (println)
          (println "This tool is to manage users, policies and apps in Site.")
          (println)
          (println summary)
          (println)
          (println (str ansi/white-font "Subcommands" ansi/reset-font))
          (println)
          (doseq [[command {:keys [description cli-options delegate]}] commands]
            (apply
             println
             (remove nil? [command (str ansi/yellow-font "(" description (when-not delegate " - coming soon!") ")" ansi/reset-font)]))
            (println (:summary (parse-opts [] cli-options)))
            (println))))]

  (cond
    (contains? options :help)
    (print-summary)

    errors
    (doseq [err errors]
      (println err))

    (find (into {} commands) command)
    (binding [*debug* (:debug options)]
      (if delegate
        (delegate
         (-> (parse-opts (next arguments) cli-options)
             (update :options merge options)))
        (println "No implementation:" command)))

    (first arguments)
    (println "Unknown command:" (first arguments))

    :else
    (print-summary)))

;; Local Variables:
;; mode: clojure
;; End:
